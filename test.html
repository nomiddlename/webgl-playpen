<!doctype html>
<html> <head>
<title>WebGL Test</title>
<script src="requestAnimationFrame-shim.js"></script>
<script src="three.js"></script>
<script src="stuff.js"></script>
<style type="text/css">
body {
  background-color: black;
  color: white;
}

#thing {
 width: 100%;
  height: 500px;
}
</style>
</head>
<body>
<h1>Messing about with WebGL</h1>
<div id="thing"></div>
<script id="vertexShader" type="x-shader/x-vertex">
attribute float displacement;
varying vec3 vNormal;
void main() {
  vNormal = normal;
  vec3 newPosition = position + (normal * vec3(displacement));
  gl_Position = projectionMatrix * modelViewMatrix * vec4(newPosition,
1.0);
}
</script>
<script id="fragmentShader" type="x-shader/x-fragment">
uniform vec3 colour;
uniform vec3 lightPosition;
varying vec3 vNormal;
void main() {

    // calc the dot product and clamp
    // 0 -> 1 rather than -1 -> 1
    vec3 light = normalize(lightPosition);
    
    // calculate the dot product of
    // the light to the vertex normal
    float dProd = max(0.0, dot(vNormal, light));
    
    // feed into our frag colour
    gl_FragColor = vec4(colour.rgb * dProd, 1.0);
    
}</script>

<script id="turbulenceVertex" type="x-shader/x-vertex">
varying vec3 vNormal;
varying vec3 vPosition;
void main() {
  vNormal = normal;
  vPosition = position;
  gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
}
</script>
<script id="turbulenceFragment" type="x-shader/x-fragment">
//
// Description : Array and textureless GLSL 2D/3D/4D simplex 
//               noise functions.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : ijm
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
// 

vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x) {
     return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(vec3 v)
  { 
  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

// First corner
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;

// Other corners
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  //   x0 = x0 - 0.0 + 0.0 * C.xxx;
  //   x1 = x0 - i1  + 1.0 * C.xxx;
  //   x2 = x0 - i2  + 2.0 * C.xxx;
  //   x3 = x0 - 1.0 + 3.0 * C.xxx;
  vec3 x1 = x0 - i1 + C.xxx;
  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

// Permutations
  i = mod289(i); 
  vec4 p = permute( permute( permute( 
             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
           + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

// Gradients: 7x7 points over a square, mapped onto an octahedron.
// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
  float n_ = 0.142857142857; // 1.0/7.0
  vec3  ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
  //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));

  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);

//Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

// Mix final noise value
  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
  m = m * m;
  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                dot(p2,x2), dot(p3,x3) ) );
  }

uniform float time;
uniform vec3 thingColour1;
uniform vec3 thingColour2;
uniform bool useLight;
uniform vec3 lightPosition;
varying vec3 vNormal;
varying vec3 vPosition;
const vec3 timeDirection = vec3(0.5, 0.5, 0.5);

void main() {
  float f = 1.0, value = 0.0, dProd = 1.0;
  vec3 normedPosition = normalize(vPosition);
  for (int i=0; i < 2; i++) {
    value += abs(snoise((normedPosition - time*timeDirection) * f)) / f;
    f = f * 2.17;
  }

  if (useLight) {
   // calc the dot product and clamp
    // 0 -> 1 rather than -1 -> 1
    vec3 light = normalize(lightPosition);
    
    // calculate the dot product of
    // the light to the vertex normal
    dProd = max(0.0, dot(vNormal, light));
  }
  // feed into our frag colour
  gl_FragColor = vec4(mix(thingColour1.rgb, thingColour2.rgb, value) *
  dProd, 1.0);
}
</script>
<script>
(function(lib) {
  doTheThing(lib, document.getElementById("thing"));
})(THREE);
</script>
</body> </html>
